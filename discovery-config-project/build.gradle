// 1. 플러그인 설정
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.10' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
}

// 2. 모든 프로젝트(Root + 하위 모듈) 공통 설정
allprojects {
	group = 'com.example'
	version = '0.0.1-SNAPSHOT'

	repositories {
		mavenCentral()
	}
}

// 3. 자식 모듈들에게만 적용할 실질적 빌드 설정
subprojects {
	// apply plugin: 'java' 대신 'java-library' (기능: implementation, api 활용 가능)
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	java {
		toolchain {
		languageVersion = JavaLanguageVersion.of(17)
		}
	}

	configurations {
		compileOnly {
		extendsFrom annotationProcessor
		}
	}

	dependencies {
		// Sptring Boot Starter Actuator (/actuator/refresh, /actuator/health 등 관리 기능)
		implementation 'org.springframework.boot:spring-boot-starter-actuator'

		// Lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		// Spring Boot Configuration Processor (application.yml 자동 완성)
		annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

		// Test
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}


	tasks.named('test') {
		useJUnitPlatform()
	}

	// Spring Cloud BOM
	ext {
		set('SpringCloudVersion', '2025.0.0')
	}

	// 각각 단독 동작을 위해 jar 끄기는 지움
	dependencyManagement {
		imports {
			// mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2025.0.0'
			// 문자열 보관을 위해 싱글'이 아닌 멀티"로 변수처리
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${property('SpringCloudVersion')}"
		}
	}

}